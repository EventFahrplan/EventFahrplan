import nerd.tuxmobil.fahrplan.congress.Android
import nerd.tuxmobil.fahrplan.congress.Libs
import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

ext.set("APP_VERSION", "${gitSha()}")

apply plugin: "com.android.application"
apply plugin: "kotlin-android"
apply plugin: "org.sonarqube"
apply from: "../sonarqube.gradle"

Properties props = new Properties()
File gradlePropertiesFile = file("gradle.properties")
if (!gradlePropertiesFile.exists()) {
    throw new IllegalStateException(
            "Please create the file 'gradle.properties' before " +
                    "importing the project. Do not forget to add custom values!"
    )
}
props.load(new FileInputStream(gradlePropertiesFile))

android {
    compileSdkVersion Android.compileSdkVersion
    buildToolsVersion Android.buildToolsVersion

    defaultConfig {
        versionCode 55
        versionName "1.38.4"
        minSdkVersion Android.minSdkVersion
        targetSdkVersion Android.targetSdkVersion
        archivesBaseName = "Fahrplan-$versionName"

        // Build information
        buildConfigField "String", "BUILD_TIME", "\"${buildTime()}\""
        buildConfigField "String", "GIT_SHA", "\"${gitSha()}\""
        buildConfigField "String", "C3NAV_URL", "\"https://35c3.c3nav.de/l/\""
    }

    buildTypes {
        debug {
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
            zipAlignEnabled true
            debuggable true
        }
        release {
            zipAlignEnabled true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'),
                    'proguard-rules/proguard-project.txt',
                    'proguard-rules/okhttp3.pro',
                    'proguard-rules/okio.pro'
        }
    }

    signingConfigs {
        camp2015 {
            storeFile file(props['signing.camp2015-release.keystoreFilePath'])
            storePassword props['signing.camp2015-release.keystorePassword']
            keyAlias props['signing.camp2015-release.keyAlias']
            keyPassword props['signing.camp2015-release.keyPassword']
        }
        ccc35c3 {
            storeFile file(props['signing.ccc35c3-release.keystoreFilePath'])
            storePassword props['signing.ccc35c3-release.keystorePassword']
            keyAlias props['signing.ccc35c3-release.keyAlias']
            keyPassword props['signing.ccc35c3-release.keyPassword']
        }
    }

    def defaultDimension = "default"
    flavorDimensions defaultDimension

    productFlavors {
        camp2015 {
            dimension defaultDimension
            applicationId "nerd.tuxmobil.fahrplan.camp"
            signingConfig signingConfigs.camp2015
            buildConfigField "String", "SCHEDULE_URL", '"https://events.ccc.de/camp/2015/Fahrplan/schedule.xml"'
            buildConfigField "String", "EVENT_URL", '"http://events.ccc.de/camp/2015/Fahrplan/events/%1$s.html"'
            buildConfigField "String", "SERVER_BACKEND_TYPE", '"frab"'
            buildConfigField "String", "VENUE", '"mildenberg"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "8"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "13"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2015"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "8"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "17"
            buildConfigField "boolean", "SHOW_APP_DISCLAIMER", "false"
            buildConfigField "boolean", "ENGAGE_GOOGLE_BETA_TESTING", "false"
            buildConfigField "boolean", "ENGAGE_GOOGLE_PLAY_RATING", "true"
            buildConfigField "String", "TRACE_DROID_EMAIL_ADDRESS", '"ENTER_EMAIL_ADDRESS_HERE"'
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.cccv.de/en/camp2015/public/events/%s/feedback/new"'

        }
        ccc35c3 {
            dimension defaultDimension
            applicationId "info.metadude.android.congress.schedule"
            signingConfig signingConfigs.ccc35c3
            buildConfigField "String", "SCHEDULE_URL", '"https://raw.githubusercontent.com/voc/35C3_schedule/master/everything.schedule.xml"'
            buildConfigField "String", "EVENT_URL", '"https://events.ccc.de/congress/2018/Fahrplan/events/%1$s.html"'
            buildConfigField "String", "SERVER_BACKEND_TYPE", '"frab"'
            buildConfigField "String", "VENUE", '"leipzig-messe"'
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_YEAR", "2018"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_MONTH", "12"
            buildConfigField "int", "SCHEDULE_FIRST_DAY_START_DAY", "27"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_YEAR", "2018"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_MONTH", "12"
            buildConfigField "int", "SCHEDULE_LAST_DAY_END_DAY", "31"
            buildConfigField "boolean", "SHOW_APP_DISCLAIMER", "false"
            buildConfigField "boolean", "ENGAGE_GOOGLE_BETA_TESTING", "true"
            buildConfigField "boolean", "ENGAGE_GOOGLE_PLAY_RATING", "true"
            buildConfigField "String", "TRACE_DROID_EMAIL_ADDRESS", '"tobias.preuss+35c3@googlemail.com"'
            buildConfigField "String", "SCHEDULE_FEEDBACK_URL", '"https://frab.cccv.de/en/35C3/public/events/%s/feedback/new"'
        }
    }

    lintOptions {
        // for okio - https://github.com/square/okio/issues/58
        warning "InvalidPackage"
    }

    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
}

tasks.withType(Test) {
    testLogging {
        events TestLogEvent.FAILED,
                TestLogEvent.PASSED,
                TestLogEvent.SKIPPED,
                TestLogEvent.STANDARD_ERROR,
                TestLogEvent.STANDARD_OUT
        exceptionFormat TestExceptionFormat.FULL
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

dependencies {
    implementation project(":database")
    implementation project(":network")

    implementation Libs.kotlinStdlib
    implementation Libs.supportLibraryAppcompatV7
    implementation Libs.supportLibraryDesign
    implementation Libs.okhttp
    implementation Libs.okhttpLoggingInterceptor
    implementation Libs.snackengagePlayrate
    implementation Libs.emailIntentBuilder
    implementation Libs.tracedroid

    testImplementation Libs.junit
    testImplementation Libs.assertjAndroid
    testImplementation Libs.supportLibraryAnnotations
    testImplementation Libs.mockitoCore
    testImplementation Libs.mockitoKotlin
    testImplementation Libs.threeTenBp
}

def gitSha() {
    def res = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
    def diff = 'git diff'.execute([], project.rootDir).text.trim()
    if (diff != null && diff.length() > 0) {
        res += "-dirty"
    }
    return res
}

def buildTime() {
    return new Date().format("yyyy-MM-dd'T'HH:mm'Z'", TimeZone.getTimeZone("UTC"))
}
